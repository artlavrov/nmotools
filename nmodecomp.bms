# 3DVIA .nmo assets extractor 0.1 by artlavrov
# script for QuickBMS http://quickbms.aluigi.org

endian little

# determine format
getct SIG string 0

if SIG != "Nemo Fi"
	print "Not an .nmo file"
	cleanexit
endif

get C_DATE long
get C_CRC long
get C_PLUGIN1 long
get C_PLUGIN2 long
get C_FLAGS long

get C_COMPCSZ long
get C_OBJCSZ long
get C_OBJSZ long
get C_ADDPATH long

get C_COMPCOUNT long
get C_OBJCOUNT long
get C_ZERO long
get C_VERSION long
get C_COMPSIZE long

comtype zlib

set OFFSET 64

if C_COMPCSZ != C_COMPSIZE
	clog MEMORY_FILE1 OFFSET C_COMPCSZ C_COMPSIZE
else
	log MEMORY_FILE1 OFFSET C_COMPSIZE
endif

math OFFSET += C_COMPCSZ

if C_OBJCSZ != C_OBJSZ
	clog MEMORY_FILE2 OFFSET C_OBJCSZ C_OBJSZ
else
	log MEMORY_FILE2 OFFSET C_OBJSZ
endif

set FNAME "decompressed.nmo"

log FNAME 0 64

append

get SIZE asize MEMORY_FILE1
log FNAME 0 SIZE MEMORY_FILE1

get SIZE asize MEMORY_FILE2
log FNAME 0 SIZE MEMORY_FILE2
