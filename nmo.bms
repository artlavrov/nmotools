# 3DVIA .nmo assets extractor 0.1 by artlavrov
# script for QuickBMS http://quickbms.aluigi.org

endian little

# determine format
getct SIG string 0

if SIG != "Nemo Fi"
	print "Not an .nmo file"
	cleanexit
endif

get C_DATE long
get C_CRC long
get C_PLUGIN1 long
get C_PLUGIN2 long
get C_FLAGS long

get C_COMPCSZ long
get C_OBJCSZ long
get C_OBJSZ long
get C_ADDPATH long

get C_COMPCOUNT long
get C_OBJCOUNT long
get C_ZERO long
get C_VERSION long
get C_COMPSIZE long

string TMP p= "crc: 0x%08x objectCount: %d componentsCount: %d" C_CRC C_OBJCOUNT C_COMPCOUNT
print "%TMP%"

comtype zlib

set OFFSET 64

if C_COMPCSZ != C_COMPSIZE
	clog MEMORY_FILE1 OFFSET C_COMPCSZ C_COMPSIZE
else
	log MEMORY_FILE1 OFFSET C_COMPSIZE
endif

math OFFSET += C_COMPCSZ

if C_OBJCSZ != C_OBJSZ
	clog MEMORY_FILE2 OFFSET C_OBJCSZ C_OBJSZ
else
	log MEMORY_FILE2 OFFSET C_OBJSZ
endif

log MEMORY_FILE3 0 0

set totalSize C_COMPSIZE
math totalSize += 64
set OFFSET 0
string NAME p= "objects/%08d_0x%08x_%s(%d)" 0 0 "PARAMETEROPERATION" 0
set TITLE = ""

for i = 0 < C_COMPCOUNT

	get C_ID long MEMORY_FILE1
	get C_TYPE long MEMORY_FILE1
	get C_OFS long MEMORY_FILE1
	get C_NAMELEN long MEMORY_FILE1
	getdstring C_NAME C_NAMELEN MEMORY_FILE1

	if C_TYPE == 1
		set TYPE string "OBJECT"
	elif C_TYPE == 2
		set TYPE string "PARAMETERIN"
	elif C_TYPE == 3
		set TYPE string "PARAMETEROUT"
	elif C_TYPE == 6
		set TYPE string "BEHAVIORLINK"
	elif C_TYPE == 8
		set TYPE string "BEHAVIOR"
	elif C_TYPE == 9
		set TYPE string "BEHAVIORIO"
	elif C_TYPE == 15
		set TYPE string "OBJECTANIMATION"
	elif C_TYPE == 23
		set TYPE string "GROUP"
	elif C_TYPE == 31
		set TYPE string "TEXTURE"
	elif C_TYPE == 33
		set TYPE string "ENTITY_3D"
	elif C_TYPE == 45
		set TYPE string "PARAMETERLOCAL"
	elif C_TYPE == 48
		set TYPE string "INTERFACEOBJECTMANAGER"
	elif C_TYPE == 52
		set TYPE string "DATAARRAY"
	else
		set TYPE string "UNKNOWN"
	endif

	set SIZE C_OFS
	math SIZE -= totalSize

	string TMP p= "%8d %8d %s \"%s\"" OFFSET SIZE NAME (TITLE)
	put TMP line MEMORY_FILE3

	log NAME OFFSET SIZE MEMORY_FILE2

	string NAME p= "objects/%08d_0x%08x_%s(%d)" i C_ID TYPE C_TYPE
	string TITLE p= "%s" C_NAME

	math OFFSET += SIZE
	math totalSize += SIZE

	next i

xmath SIZE "C_OBJSZ - C_OFS + C_COMPSIZE + 64"
string TMP p= "%8d %8d %s \"%s\"" OFFSET SIZE NAME (TITLE)
put TMP line MEMORY_FILE3
log NAME OFFSET SIZE MEMORY_FILE2

#get SIZE asize MEMORY_FILE1
#log "components.bin" 0 SIZE MEMORY_FILE1

#get SIZE asize MEMORY_FILE2
#log "objects.bin" 0 SIZE MEMORY_FILE2

get SIZE asize MEMORY_FILE3
log "objects.txt" 0 SIZE MEMORY_FILE3

